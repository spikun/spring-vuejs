group 'ee.korogui'
version '0.1'

wrapper.gradleVersion = '3.5'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.9.RELEASE'
        classpath group: 'com.moowork.gradle', name: 'gradle-node-plugin', version: '1.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "com.moowork.node"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: '1.5.9.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

node {
    nodeModulesDir = file("client")
}

task buildClient(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = "Compile client side folder"
    args = ['run', 'build']
}
task buildClientWatch(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = "Build and watches the client side assets for rebuilding"
    args = ['run', 'watch']
}

bootRun.dependsOn(buildClient)
jar.dependsOn(buildClient)
